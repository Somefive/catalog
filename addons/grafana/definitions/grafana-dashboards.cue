import (
    "encoding/json"
    "strings"
    "list"
    "strconv"
)

"grafana-dashboards": {
    alias: ""
    annotations: {}
    attributes: podDisruptive: false
    description: "Build grafana dashboards."
    labels: "ui-hidden": "true"
    type: "trait"
}

template: {
	parameter: {
        dashboards: [...#DashboardTemplate]
        name: string
        namespace: *"o11y-system" | string
    }
    outputs: "grafana-dashboards.\(parameter.namespace).\(parameter.name)": {
        apiVersion: "v1"
        kind: "ConfigMap"
        metadata: {
            name: "grafana-dashboards.\(parameter.name)"
            namespace: parameter.namespace
            labels: "o11y.oam.dev/config": "grafana-dashboard-model"
        }
        data: {
            for dashboard in parameter.dashboards {
                "\(dashboard.uid).json": json.Marshal({
                    for k, v in dashboard {
                        if !list.Contains(["panelParams", "defaultVariables", "autoTemplating", "variables"], k) {
                            "\(k)": v
                        }
                    }
                })
            }
        }
    }

    #TemplatingVariable: {
        name: string
        label?: string
        promql?: string
    }

    #PanelParams: {
        title: string
        size: *[6, 8] | [number, number]
        pos: [number, number]
        promql?: [...{
            name?: string
            raw?: string
            metrics: string
            filter?: string
            ignoreDashboardFilter?: true
            sumBy?: [...string]
            rate: *false | bool
            quantiles: *[] | [...number]
            quantile_avg: *false | bool
        }]
        row?: collapsed: *false | true
        graph?: {
            yFormat: *"short" | string
            yLabel: *"" | string
            yMin: *"0" | string
            legendTable: *[] | [...string]
            rightSide?: true
            sortBy: *"avg" | string
        }
        unit?: string
        stat?: {
            plain: *false | bool
        }
        gauge?: true
        table?: true
        transformations?: [...{...}]
        overrides?: [...{...}]
    }

    #DashboardTemplate: {
        title: string
        uid: strings.Replace(strings.ToLower(title), " ", "-", -1)
        description: *"" | string
        time: {
            from: *"now-1h" | string
            to: *"now" | string
        }
        refresh: *"30s" | string
        defaultVariables?: [string]: string
        prometheusTemplating: *[] | [...string]
        variables: *[] | [...#TemplatingVariable]

        _datasourceType: *"prometheus" | string
        _datasourceTemplating: {
            type: "datasource"
            name: "datasource"
            label: "Data Source"
            query: _datasourceType
        }
        _rateIntervalTemplating: {
            type: "interval"
            name: "rate_interval"
            label: "Rate"
            query: "3m,5m,10m,30m"
        }
        _autoGeneratedTemplating: *[] | [...{...}]
        _dashboardFilter: strings.Join([for key in prometheusTemplating {"\(key)=~\"$\(key)\""}], ",")
        if _datasourceType == "prometheus" {
            _autoGeneratedTemplating: [for key in prometheusTemplating {
                type: "query"
                name: key
                if strings.Contains(key, "_") {
                    label: key
                }
                if !strings.Contains(key, "_") {
                    label: strings.ToTitle(key)
                }
                query: {
                    query: "label_values(up, \(key))"
                    refId: "StandardVariableQuery"
                }
                refresh: 1
                datasource: uid: "${datasource}"
            }]
        }        
        _templatings: [_datasourceTemplating] + _autoGeneratedTemplating + [for var in variables {
            name: var.name
            if var.label != _|_ {
                label: var.label
            }
            if var.label == _|_ {
                label: strings.ToTitle(name)
            }
            if var.promql != _|_ {
                query: {
                    query: var.promql
                    refId: "StandardVariableQuery"
                }
                refresh: 1
                datasource: uid: "${datasource}"
            }
        }] + [_rateIntervalTemplating]
        templating: list: [for _templating in _templatings {
            _templating
            if defaultVariables != _|_ && defaultVariables[_templating.name] != _|_ {
                current: {
                    selected: true
                    text: defaultVariables[_templating.name]
                    value: defaultVariables[_templating.name]
                }
            }
        }]

        _datasource: {
            uid: "${datasource}"
            type: _datasourceType
        }

        panelParams: [...#PanelParams]
        panels: [for idx, param in panelParams {
            title: param.title
            if param.transformations != _|_ {
                transformations: param.transformations
            }
            if param.overrides != _|_ {
                fieldConfig: overrides: param.overrides
            }

            if param.promql != _|_ {
                type: *"graph" | string
                if param.unit != _|_ {
                    fieldConfig: defaults: unit: param.unit
                }
                if param.stat != _|_ {
                    type: "stat"
                    if param.stat.plain {
                        options: graphMode: "none"
                        options: colorMode: "none"
                    }
                }
                if param.gauge != _|_ {
                    type: "gauge"
                    fieldConfig: defaults: {
                        color: mode: "thresholds"
                        unit: "percentunit"
                        min: 0
                        max: 1
                        thresholds: steps: [{
                            color: "green"
                            value: null
                        }, {
                            color: "yellow"
                            value: 0.6
                        }, {
                            color: "red"
                            value: 0.8
                        }]
                    }
                }
                if param.table != _|_ {
                    type: "table"
                }
                datasource: _datasource
                _targets: [for query in param.promql {
                    _filter: "{" + strings.Join([for f in [{
                        if query.ignoreDashboardFilter == _|_ {filter: _dashboardFilter}
                    }, {
                        if query.filter != _|_ {filter: query.filter}
                    }] if f.filter != _|_ {f.filter}], ",") + "}"
                    filteredMetrics: "\(query.metrics)\(_filter)"
                    ratedMetrics: *"" | string
                    if query.rate {
                        ratedMetrics: "rate(\(filteredMetrics)[$rate_interval])"
                    }
                    if !query.rate {
                        ratedMetrics: filteredMetrics
                    }
                    sumedMetrics: *"" | string
                    _legendFormat: *"" | string
                    if query.name != _|_ {
                        _legendFormat: query.name
                    }
                    if query.sumBy != _|_ {
                        _sumBy: strings.Join(query.sumBy, ",")
                        sumedMetrics: "sum(\(ratedMetrics)) by (\(_sumBy))"
                        if len(query.sumBy) > 0 {
                            _legendFormat: strings.Join([for key in query.sumBy {"{{\(key)}}"}], " ")
                        }
                    }
                    if query.sumBy == _|_ {
                        sumedMetrics: ratedMetrics
                    }
                    if len(query.quantiles) == 0 && !query.quantile_avg {
                        outputs: [{
                            if query.raw != _|_ {
                                expr: query.raw
                            }
                            if query.raw == _|_ {
                                expr: sumedMetrics
                            }
                            legendFormat: _legendFormat
                            range: true
                        }]
                    }
                    if query.quantile_avg {
                        outputs: [{
                            _sumBy: *"" | string
                            legendFormat: *"avg" | string
                            if query.name != _|_ {
                                legendFormat: query.name
                            }
                            if query.sumBy != _|_ && query.name == _|_ {
                                _sumBy_: strings.Join(query.sumBy, ",")
                                _sumBy: " by (\(_sumBy_))"
                                legendFormat: strings.Join([for key in query.sumBy {"{{\(key)}}"}], " ")
                            }
                            expr: "sum(rate(\(query.metrics)_sum\(_filter)[$rate_interval]))\(_sumBy) / sum(rate(\(query.metrics)_count\(_filter)[$rate_interval]))\(_sumBy)"
                            range: true
                        }]
                    }
                    if len(query.quantiles) > 0 {
                        outputs: [for q in query.quantiles {
                            _q: strconv.FormatInt(q, 10)
                            expr: "histogram_quantile(0.\(_q), sum(rate(\(query.metrics)_bucket\(_filter)[$rate_interval])) by (le))"
                            legendFormat: "p\(_q)"
                            range: true
                        }] + [{
                            expr: "sum(rate(\(query.metrics)_sum\(_filter)[$rate_interval])) / sum(rate(\(query.metrics)_count\(_filter)[$rate_interval]))"
                            legendFormat: "avg"
                            range: true
                        }]
                    }
                }]
                targets: [for target in _targets for output in target.outputs {
                    output
                    if param.table != _|_ { format: "table" }
                }]
            }
            _isRow: param.promql == _|_
            if _isRow {
                type: "row"
            }
            gridPos: {
                x: param.pos[0]
                y: param.pos[1]
                if _isRow {
                    w: 24
                    h: 1
                }
                if !_isRow {
                    w: param.size[0]
                    h: param.size[1]
                }
            }
            if param.graph != _|_ {
                yaxes: [{
                    format: param.graph.yFormat
                    if param.graph.yLabel != "" {
                        label: param.graph.yLabel
                    }
                    if param.graph.yMin != "" {
                        min: param.graph.yMin
                    }
                    logBase: 1
                    show: true
                }, {
                    format: param.graph.yFormat
                    logBase: 1
                    show: true
                }]
                if len(param.graph.legendTable) > 0 {
                    legend: {
                        alignAsTable: true
                        rightSide: param.size[0] > 6 || param.graph.rightSide != _|_
                        for key in ["avg", "current", "max", "min", "total"] {
                            "\(key)": list.Contains(param.graph.legendTable, key)
                        }
                        sort: param.graph.sortBy
                        sortDesc: true
                        values: true
                    }
                }
            }
        }]
    }
}
